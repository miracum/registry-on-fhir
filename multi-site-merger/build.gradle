plugins {
    id 'org.springframework.boot' version '2.7.6'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.8.0'
}

group = 'org.miracum.registry'
version = '2.1.10' // x-release-please-version
sourceCompatibility = '17'
targetCompatibility = '17'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.retry:spring-retry:1.3.4'

    implementation 'org.jgrapht:jgrapht-core:1.5.1'
    implementation 'org.jgrapht:jgrapht-io:1.5.1'

    implementation 'ca.uhn.hapi.fhir:hapi-fhir-base:5.5.1'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-client:5.5.1'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:5.5.1'

    implementation 'io.micrometer:micrometer-registry-prometheus:1.9.5'
    implementation 'io.micrometer:micrometer-core:1.9.5'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.2'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled true
        csv.destination file("${buildDir}/jacoco/coverage.csv")
        html.destination file("${buildDir}/jacoco/html")
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}

jar {
    enabled = false
}

checkstyle {
    toolVersion '8.45'
    configFile = file('.checkstyle/checkstyle.xml')
}
